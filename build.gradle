plugins {
    id "com.jfrog.artifactory" version "4.8.1"
}

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply from: 'hmrc_bintray_distribution_plugin.gradle'

project.ext {
    jobDslVersion = '1.69'
    jenkinsVersion = '2.89'
    testDependenciesOutputDirectory = new File(sourceSets.test.output.resourcesDir, 'test-dependencies')
    artifactoryUri = System.getenv("ARTIFACTORY_URI")
    publishingRepoKey = getArtifactoryRepoKey()
    publishingRepoUsername = System.getenv("PUBLISHING_REPO_USERNAME")
    publishingRepoPassword = System.getenv("PUBLISHING_REPO_PASSWORD")
}

configurations {
    testPlugins {}

    // see JENKINS-45512
    testCompile {
        exclude group: 'xalan'
        exclude group: 'xerces'
    }
}

sourceSets {
    test {
        resources {
            srcDirs = ['src/test/resources', sourceSets.test.output.resourcesDir]
        }
    }
}

repositories {
    maven { url 'https://repo.jenkins-ci.org/public/' }
    maven { url 'https://repo.jenkins-ci.org/releases/' }
    jcenter()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.12'
    compile ('com.google.guava:guava:11.0.1') {
        exclude module: 'jsr305'
    }
    compile ('org.jenkins-ci.plugins:job-dsl-core:1.71') {
        exclude module: 'ivy'
    }

    testCompile 'org.spockframework:spock-core:1.1-groovy-2.4'

    // Jenkins test harness dependencies
    testCompile 'org.jenkins-ci.main:jenkins-test-harness:2.33'
    testCompile "org.jenkins-ci.main:jenkins-war:${jenkinsVersion}"

    // Job DSL plugin including plugin dependencies
    testCompile "org.jenkins-ci.plugins:job-dsl:${jobDslVersion}"
    testCompile "org.jenkins-ci.plugins:job-dsl:${jobDslVersion}@jar"
    testCompile 'org.jenkins-ci.plugins:structs:1.6@jar'

    // plugins to install in test instance
    testPlugins 'org.jenkins-ci.plugins:matrix-auth:2.2'
    testPlugins 'org.jenkins-ci.plugins:cloudbees-folder:6.4'
    testPlugins 'org.jenkins-ci.plugins:token-macro:2.5'
    testPlugins 'org.jenkins-ci.plugins:ghprb:1.42.0'
}

version = getReleaseVersion()
logger.quiet("Project version is: ${project.version}")
group = 'uk.gov.hmrc'

task resolveTestPlugins(type: Copy) {
    from configurations.testPlugins
    into testDependenciesOutputDirectory
    include '*.hpi'
    include '*.jpi'
    mustRunAfter compileJava
}

task writePluginIndexFile(type: DefaultTask, dependsOn: resolveTestPlugins) {
    doFirst {
        def baseNames = resolveTestPlugins.source.collect {
            it.name[0..it.name.lastIndexOf('.') - 1]
        }.sort()
        logger.info("Copied plugins for tests.")

        new File(resolveTestPlugins.destinationDir, 'index').setText(baseNames.join('\n'), 'UTF-8')
        logger.info("Created plugin index file.")
    }
}

test {
    dependsOn tasks.writePluginIndexFile
    inputs.files sourceSets.test.groovy.srcDirs, sourceSets.test.output.resourcesDir

    // set build directory for Jenkins test harness, JENKINS-26331
    systemProperty 'buildDirectory', project.buildDir.absolutePath
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

publishing {
    publications {
        jenkinsJobBuilders(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

artifactoryDeploy {
    doFirst {
        validatePublishingConfiguration()
    }
}

artifactory {
    contextUrl = project.artifactoryUri
    publish {
        repository {
            repoKey = project.publishingRepoKey
            username = project.publishingRepoUsername
            password = project.publishingRepoPassword
        }
        defaults {
            publications('jenkinsJobBuilders')
        }
        publishBuildInfo = true
        publishArtifacts = true
        publishPom = true
    }
}

bintrayDistribution {
    artefactName = "jenkins-job-builders"
    artefactVersion = getReleaseVersion()
}

private validatePublishingConfiguration() {
    boolean isReleaseVersion = isReleaseVersion()
    final Set<String> requiredPublishingEnvVars = ["ARTIFACTORY_URI", "PUBLISHING_REPO_USERNAME", "PUBLISHING_REPO_PASSWORD"]
    if(isReleaseVersion) {
        requiredPublishingEnvVars << "RELEASES_PUBLISHING_REPO"
    }
    else {
        requiredPublishingEnvVars << "SNAPSHOTS_PUBLISHING_REPO"
    }
    requiredPublishingEnvVars.each { envVar ->
        if(!System.getenv(envVar)) {
            throw new GradleException("Environment variable '${envVar}' is required to publish ${isReleaseVersion ? "releases" : "snapshots"} to Artifactory, but was not provided.")
        }
    }
}

private String getReleaseVersion() {
    return project.hasProperty("releaseVersion") ? project.releaseVersion.replaceAll('release/', '') : 'unnumbered-development-build'
}

private boolean isReleaseVersion() {
    final boolean isReleaseVersion = (getReleaseVersion() ==~ /^[0-9]+\.[0-9]+\.[0-9]+$/)
    logger.quiet("Specified releaseVersion '${getReleaseVersion()}' has been identified as a ${isReleaseVersion ? "release" : "snapshot"} version.")
    return isReleaseVersion
}

private String getArtifactoryRepoKey() {
    final String repoKeyEnvVar = isReleaseVersion() ? "RELEASES_PUBLISHING_REPO" : "SNAPSHOTS_PUBLISHING_REPO"
    final String repoKey = System.getenv(repoKeyEnvVar)
    logger.quiet("Artifactory repo key is: ${repoKey}")
    return repoKey
}

tasks.install.dependsOn(build)
tasks.artifactoryPublish.dependsOn(check)
tasks.distributeToBintray.dependsOn(artifactoryPublish)
