apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: "com.jfrog.bintray"

buildscript {
    repositories {
        maven { url 'http://jcenter.bintray.com'}
        maven { url 'http://dl.bintray.com/hmrc/releases'}
    }
    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.1"
    }
}

repositories {
    maven { url 'http://jcenter.bintray.com'}
}

dependencies {
    compile 'org.codehaus.groovy:groovy:2.1.3'
    compile ('com.google.guava:guava:11.0.1') {
        exclude module: 'jsr305'
    }
    compile ('org.jenkins-ci.plugins:job-dsl-core:1.29') {
        exclude module: 'ivy'
    }
    testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
    testCompile 'org.jenkins-ci:version-number:1.1'
}

version = '10.25.0'
group = 'uk.gov.hmrc'

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

bintray {
    user = credentialsProperty('user')
    key = credentialsProperty('password')
    publications = ['mavenJava']
    pkg {
        repo = 'releases'
        name = this.project.name
        userOrg = 'hmrc'
        version {
            name = this.version
            vcsTag = 'None'
        }
    }
    publish = true
}

private def credentialsProperty(String key) {
    def credentials = new Properties()
    def file = new File("${System.properties['user.home']}", '/.bintray/.credentials')
    if (file.exists()) {
        credentials.load(file.newDataInputStream())
    }
    credentials.get(key)
}
